{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "# ----------------------- OLD METHOD USING CLASS NUMBER -------------------------\n",
    "# Function Kr_from_quartic: computes composite fields of totally real non-normal quartic fields and\n",
    "# imaginary quadratic fields of class number 1,2,4.\n",
    "# INPUT: (list quartic fields, list imaginary quadratic fields)\n",
    "# OUTPUT: list with elements [polynomial of Kr, dKr/dKrplus^2, hKrstar]\n",
    "def Kr_from_quartic(quartic,iq):\n",
    "    list_Kr = []\n",
    "    check_isom = 0\n",
    "    for quart in quartic:\n",
    "        Krplus.<f0> = NumberField(quart[1])\n",
    "        R0.<t0> = PolynomialRing(Krplus)\n",
    "        hKrplus = Krplus.class_number()\n",
    "        dKrplus = Krplus.discriminant()\n",
    "        for quadr in iq:\n",
    "            kKrplus.<f1> = Krplus.extension(quadr[1])\n",
    "            Kr.<f> = kKrplus.absolute_field()\n",
    "            dKr = Kr.discriminant()\n",
    "            d = dKr/dKrplus^2\n",
    "            if  d == 1:\n",
    "                if len(list_Kr) == 0:\n",
    "                    hKr = Kr.class_number()\n",
    "                    hKrstar = hKr/hKrplus\n",
    "                    list_Kr.append([Kr.absolute_polynomial(),d, hKrstar])\n",
    "                else:\n",
    "                    for m in list_Kr:\n",
    "                        n.<b> = NumberField(m[0])\n",
    "                        if Kr.is_isomorphic(n) == True:\n",
    "                            check_isom = 1\n",
    "                            break\n",
    "                    if check_isom == 0:\n",
    "                        hKr = Kr.class_number()\n",
    "                        hKrstar = hKr/hKrplus\n",
    "                        list_Kr.append([Kr.absolute_polynomial(),d, hKrstar])\n",
    "                    else:\n",
    "                        check_isom = 0\n",
    "    return list_Kr\n",
    "\n",
    "# Function compute_K_from_Kr computes sextic fields K from reflex fields Kr up to isomorphism\n",
    "# INPUT: list of non-normal octic reflex fields Kr with non-normal subfield Krplus\n",
    "# OUTPUT: list with [K,Kr,Phir] where K is the sextic reflex field of Kr and Phir the CM-type\n",
    "def compute_K_from_Kr(list_Kr):\n",
    "    list_sextic = []\n",
    "    check_isom = 0\n",
    "    for Kr in list_Kr:\n",
    "        Krcm = CM_Field(Kr[0])\n",
    "        Phir_set = Krcm.CM_types()\n",
    "        for Phir in Phir_set:\n",
    "            K = Phir.reflex_field()\n",
    "            if K.g() == 3:\n",
    "                if len(list_sextic) == 0:\n",
    "                    list_sextic.append([K,Krcm,Phir])\n",
    "                else:\n",
    "                    N.<a> = NumberField(K.polynomial())\n",
    "                    for k in list_sextic:\n",
    "                        n.<b> = NumberField(k[0].polynomial())\n",
    "                        if N.is_isomorphic(n) == True:\n",
    "                            check_isom = 1\n",
    "                            break\n",
    "                    if check_isom == 0:\n",
    "                        list_sextic.append([K,Krcm,Phir])\n",
    "                    else:\n",
    "                        check_isom = 0\n",
    "    return list_sextic\n",
    "\n",
    "# ----------------------- NEW METHOD FOR ALL QUADRATICS SUCH THAT dk|dKrplus -------------------------\n",
    "\n",
    "# Function construct_Kr: computes composite field Kr = kKrplus with all k such that dk|dKrplus\n",
    "# INPUT: LMFDB output list of quartic fields\n",
    "# OUTPUT: list with elements [polynomial of Kr, polynomial of k, class number of Krplus]\n",
    "def construct_Kr(quartic):\n",
    "    Kr_list = []\n",
    "    for quart in quartic:\n",
    "        Krplus.<f0> = NumberField(quart[1])\n",
    "        R0.<t0> = PolynomialRing(Krplus)\n",
    "        dKrplus = Krplus.discriminant()\n",
    "        kprimes = list(dKrplus.factor())\n",
    "        for i in range(1,len(kprimes)+1):\n",
    "            check_isom = 0\n",
    "            if i == 1:\n",
    "                prime_combos = kprimes\n",
    "                for p in prime_combos:\n",
    "                    k.<x> = QuadraticField(-p[0])\n",
    "                    dk = k.discriminant()\n",
    "                    if dKrplus % dk == 0:\n",
    "                        kKrplus.<y> = Krplus.extension(k.polynomial())\n",
    "                        Kr.<z> = kKrplus.absolute_field()\n",
    "                        Kr_list.append([pari(Kr.polynomial()).polredabs(),pari(k.polynomial()).polredabs(),Krplus.class_number()])\n",
    "            else:\n",
    "                prime_combos = list(combinations(kprimes,i))\n",
    "                for p in prime_combos:\n",
    "                    d = 1\n",
    "                    for n in range(1,i+1):\n",
    "                        d = d * p[i-1][0]\n",
    "                    k.<x> = QuadraticField(-d)\n",
    "                    dk = k.discriminant()\n",
    "                    if dKrplus % dk == 0:\n",
    "                        kKrplus.<y> = Krplus.extension(k.polynomial())\n",
    "                        Kr.<z> = kKrplus.absolute_field()\n",
    "                        Kr_list.append([pari(Kr.polynomial()).polredabs(),pari(k.polynomial()).polredabs(),Krplus.class_number()])\n",
    "    return Kr_list\n",
    "\n",
    "# Can this be made more efficient with isomorphism check?\n",
    "# Function compute_K_from_Kr_sub computes sextic fields K from reflex fields Kr up to isomorphism\n",
    "# INPUT: list of non-normal octic reflex fields Kr with non-normal subfield Krplus\n",
    "# OUTPUT: list with [Kr, Phir, K, k, class number of Krplus]\n",
    "def compute_K_from_Kr_sub(list_Kr):\n",
    "    list_sextic = []\n",
    "    check_isom = 0\n",
    "    for Kr in list_Kr:\n",
    "        Krcm = CM_Field(Kr[0])\n",
    "        k = CM_Field(Kr[1])\n",
    "        Phir_set = Krcm.CM_types()\n",
    "        for Phir in Phir_set:\n",
    "            K = Phir.reflex_field()\n",
    "            if K.g() == 3:\n",
    "                if len(list_sextic) == 0:\n",
    "                    list_sextic.append([Krcm,Phir,K,k,Kr[2]])\n",
    "                else:\n",
    "                    N.<a> = NumberField(K.polynomial())\n",
    "                    for m in list_sextic:\n",
    "                        n.<b> = NumberField(m[0].polynomial())\n",
    "                        if N.is_isomorphic(n) == True:\n",
    "                            check_isom = 1\n",
    "                            break\n",
    "                    if check_isom == 0:\n",
    "                        list_sextic.append([Krcm,Phir,K,k,Kr[2]])\n",
    "                    else:\n",
    "                        check_isom = 0\n",
    "    return list_sextic\n",
    "\n",
    "\n",
    "# ----------------------- CHECK IF SEXTIC FIELD IS OF CORRECT FORM -------------------------\n",
    "def check_list_K(list_sextic):\n",
    "    # Check if sextic fields with polynomial in list_sextic are of type I:\n",
    "    # not Galois, no imaginary quadratic subfield and Galois maximally totally real subfield\n",
    "    list_K = []\n",
    "    for K in list_sextic:\n",
    "        Knf.<a> = NumberField(K[0].polynomial())\n",
    "        if Knf.is_galois() == False:\n",
    "            if all(k[0].degree()!=2 for k in Knf.subfields()):\n",
    "                for Kplus in Knf.subfields():\n",
    "                    if Kplus[0].degree() == 3:\n",
    "                        if Kplus[0].is_galois() == true:\n",
    "                            if len(Kplus[0].real_embeddings()) == 3:\n",
    "                                list_K.append(K) \n",
    "    return list_K\n",
    "\n",
    "# ----------------------- FIRST CHECK CM ONE THEN COMPUTE K ---------------------------\n",
    "\n",
    "def CM_one_sextic_from_Kr(Kr_list):\n",
    "    K_list = []\n",
    "    for Kr_pol in Kr_list:\n",
    "        Kr = CM_Field(Kr_pol[0])\n",
    "        clKr = Kr.class_group()\n",
    "        gens = clKr.gens()\n",
    "        rep_gens = []\n",
    "        print(Kr_pol[0])\n",
    "        for I in gens:\n",
    "            M = Kr.minkowski_bound().numerical_approx().ceil()\n",
    "            Ip = I.representative_prime(norm_bound=M)\n",
    "            rep_gens.append(Ip)\n",
    "        Phir_set = Kr.CM_types(equivalence_classes=True)\n",
    "        for Phir in Phir_set:\n",
    "            if test_CM_cl_nr_one_with_bach_bound(Kr, Phir) == True:\n",
    "                if test_CM_cl_nr_one_with_class_group(rep_gens, Phir) == True:\n",
    "                    K = Phir.reflex_field()\n",
    "                    print(K.polynomial())\n",
    "                    if K.g() == 3:\n",
    "                        K_list.append([K.polynomial(),Kr_pol[0],Kr_pol[1],Kr_pol[2]])\n",
    "    return (K_list)\n",
    "\n",
    "# ----------------------- CM CLASS NUMBER ONE FUNCTION WITH BACH BOUND -------------------------\n",
    "\n",
    "# INPUT: (Kr, CM type Phir)\n",
    "# OUTPUT: True if CM class number one under GRH, False otherwise\n",
    "def test_CM_cl_nr_one_with_bach_bound(Kr, Phir):\n",
    "    \"\"\"\n",
    "    Test under GRH whether K has CM class number one.\n",
    "    The output True means that either K has CM class number one or GRH is false.\n",
    "    The output False means that K does not have CM class number one.\n",
    "\n",
    "    Uses splitting of small primes as well as Weil Q-number enumeration.\n",
    "    \n",
    "    \"\"\"\n",
    "    bach_bound = floor(Kr.bach_bound()) \n",
    "    for l in prime_range(bach_bound + 1): \n",
    "        for l_1 in Kr.factor(l):\n",
    "            norm_l_1 = l_1[0].norm()\n",
    "            if norm_l_1 <= bach_bound:\n",
    "                if a_to_mu(Phir, l_1[0]) is None: \n",
    "                    return False \n",
    "    return True\n",
    "    \n",
    "# ----------------------- CHECK IF FIELD ISOMORPHIC TO OTHER FIELDS IN LIST ------------------------\n",
    "# Check if list contains isomorphic K\n",
    "# INPUT: (CM-field K, list of polynomials of computed fields K)\n",
    "# OUTPUT: \"True\" if there are no isomorphic K in the existing list, \"False\" if there are\n",
    "def check_isomorphic_K_in_list(K,list_sextic):\n",
    "    check_isom = 0\n",
    "    if len(list_sextic) == 0:\n",
    "        return \"True\"\n",
    "    else:\n",
    "        N.<a> = NumberField(K.polynomial())\n",
    "        for k in list_sextic:\n",
    "            n.<b> = NumberField(k[0].polynomial())\n",
    "            if N.is_isomorphic(n) == True:\n",
    "                check_isom = 1\n",
    "                break\n",
    "    if check_isom == 0:\n",
    "        return \"True\"\n",
    "    return \"False\"\n",
    "# ----------------------- CM CLASS NUMBER ONE FUNCTION -------------------------\n",
    "# Function test_CM_cl_nr_one_with_class_group: test if K is a CM-class number one field\n",
    "# INPUT: (prime representatives of generators of Cl_Kr, CM-type Phir of Kr)\n",
    "# OUTPUT: True if CM-class number one field.\n",
    "def test_CM_cl_nr_one_with_class_group(rep_gens, Phir):\n",
    "    \"\"\"\n",
    "    Checks whether the given non-biquadratic quartic CM field is a PQ1-field.\n",
    "    (In other words, checks whether I_0(Phir)/P_{Kr} = Cl_{Kr}.)\n",
    "\n",
    "    This function uses the class group of K, hence is only fast for fields K of small discriminant.\n",
    "    We use it only after first convincing ourselves in other ways whether that the answer will be 'yes'.\n",
    "\n",
    "    This is Step 3 in Algorithm 3. # TODO: check later whether these references are correct.\n",
    "\n",
    "    INPUT: \n",
    "\n",
    "    '' Kr '' - non-biquadratic quartic CM field\n",
    "    '' Phir '' - is a CM type of Kr\n",
    "    '' K '' - the reflex field of (Kr ,Phir)\n",
    "\n",
    "    OUTPUT: returns True if K is a PQ1 field\n",
    "    \n",
    "    METHOD: \n",
    "    \n",
    "    Step 1. Compute the class group of $Kr$\n",
    "    Step 2. For each prime ideal representatives \"Ip\" of the classes compute N_{Phir}(Ip) and check if it is\n",
    "    generated by a Weil N(Ip)-number as follows.\n",
    "    a. First check whether it is a principal ideal.\n",
    "    b. If yes, say N_{Phir}(Ip) = (alpha0), then v = compute alpha0*alpha0bar / N(Ip).\n",
    "    c. Now we need to check whether the totally positive unit v is a relative norm from K to K_plus.\n",
    "       We can easily do this using a_to_mu from the ReCip package,\n",
    "       but as O_K^* = mu_K*O_{K_plus}^*, [13, Lemma 1] # TODO: update reference later as [13] can get a different number when we make changes.\n",
    "       it is also equivalent to being a square in K_plus, which is equivalent to being a square in K.\n",
    "       So we test whether v is a square.\n",
    "    \"\"\"\n",
    "    \n",
    "#     clKr = Kr.class_group()\n",
    "#     gens = clKr.gens()\n",
    "#     for I in gens:\n",
    "#         M = Kr.minkowski_bound().numerical_approx().ceil()\n",
    "#         Ip = I.representative_prime(norm_bound=M)\n",
    "    for Ip in rep_gens:\n",
    "        if a_to_mu(Phir, Ip) is None: \n",
    "            return False \n",
    "    return True\n",
    "\n",
    "# ----------------------- COMPUTE CM CLASS NUMBER ONE FIELDS K -------------------------\n",
    "# INPUT: List of sextic reflex fields K of Kr\n",
    "# OUTPUT: List of CM-class number one K [polynomial of K, hK, hk, hKr]\n",
    "def K_cm_clno_one_list(K_list):\n",
    "    list_CM_one = []\n",
    "    for tup in K_list:\n",
    "        Kr = tup[0]; Phir = tup[1]; K = tup[2]; k = tup[3]; hKrplus = tup[4]\n",
    "        if test_CM_cl_nr_one_with_class_group(Kr,Phir) == True:\n",
    "            hK = K.class_number()\n",
    "            hKr = Kr.class_number()\n",
    "            hKrstar = hKr/hKrplus\n",
    "            hk = k.class_number()\n",
    "            list_CM_one.append([K.polynomial(),hK,hk,hKrstar])\n",
    "    return(list_CM_one)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.2",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
